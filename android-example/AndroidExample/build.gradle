apply plugin: 'android'
apply from: rootProject.file('gradle/signing.gradle')

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile project(':AndroidLibrary')
    compile 'com.android.support:support-v4:18.0.0'
    compile 'org.slf4j:slf4j-api:1.7.5'
    compile 'com.github.tony19:logback-android-core:1.0.10-2'
    compile 'com.github.tony19:logback-android-classic:1.0.10-2'
}

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        versionCode 1
        versionName '1.0'
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            packageNameSuffix ".debug"
            buildConfigField "boolean", "SHOW_GREETINGS", "Boolean.parseBoolean(\"true\")"
            buildConfigField "String", "GREETINGS", "\"Greetings, My Lord !!!\""
        }
        release {
            runProguard true
            proguardFile getDefaultProguardFile('proguard-android-optimize.txt')
            proguardFile '../proguard-project.txt'
            signingConfig signingConfigs.releaseKeystore
            buildConfigField "boolean", "SHOW_GREETINGS", "Boolean.parseBoolean(\"false\")"
            buildConfigField "String", "GREETINGS", "\"Hello, World !!!\""
        }
    }

    aaptOptions {
        noCompress 'txt'
        ignoreAssetsPattern "!.svn:!.git:!.ds_store:!*.scc:.*:<dir>_*:!CVS:!thumbs.db:!picasa.ini:!*~"
    }

    lintOptions {
        abortOnError true
        checkAllWarnings true
        warningsAsErrors false
        disable 'ContentDescription', 'InvalidPackage', 'SelectableText', 'SpUsage'
    }

    packagingOptions { exclude 'META-INF/LICENSE.txt' }
}
