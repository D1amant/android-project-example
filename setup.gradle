buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.ajoberstar:gradle-git:0.6.3'
    }
}

File generateProjectDirectory(String projectName) {
    if (!(projectName ==~ /[a-zA-Z][a-zA-Z0-9]*/)) {
        throw new InvalidUserDataException("project name ($projectName) should match [a-zA-Z][a-zA-Z0-9]* pattern")
    }

    def matcher = projectName =~ /[A-Z][a-z0-9]*/
    String directoryName = matcher.collect({ (it.length() > 1 ? '-' : '') + it.toLowerCase() }).join() - '-'
    return new File(System.getProperty('user.dir'), directoryName)
}

project.ext.projectName = project.hasProperty('instanceName') ? project.getProperty('instanceName') : 'Example'
project.ext.projectDirectory = generateProjectDirectory(projectName)
project.ext.tmpDirectory = File.createTempDir(projectName, '')
project.ext.gitRepository = 'https://github.com/noveogroup/android-project-example.git'
project.ext.gitProject = project.hasProperty('projectType') ? project.getProperty('projectType') : 'android-simple-example'
project.ext.gitModule = 'example'

task create(type: org.ajoberstar.gradle.git.tasks.GitClone) {
    uri = gitRepository
    destinationPath = tmpDirectory
    bare = false
} << {
    copy {
        from new File(tmpDirectory, gitProject)
        into projectDirectory
    }
    delete tmpDirectory

    new File(projectDirectory, gitModule).renameTo(new File(projectDirectory, projectName))

    File settingsFile = new File(projectDirectory, 'settings.gradle')
    String settingsContent = settingsFile.getText()
    settingsFile.delete()
    settingsFile << settingsContent.replaceAll(gitModule, projectName)
}
